buildscript {
	ext {
		springBootVersion = '1.5.4.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compileOnly('org.projectlombok:lombok')
	compile('org.springframework.boot:spring-boot-starter')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

sourceSets {
	main {
		java.srcDir "src/main/java"
		resources.srcDir "src/main/resources"
	}
	test {
		java.srcDir "src/test/java"
	}
	testIntegration {
		java.srcDir "src/test-integration/java"

		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	testIntegrationCompile.extendsFrom testCompile
	testIntegrationRuntime.extendsFrom testRuntime
}

task testIntegration(type: Test) {
	testClassesDir = sourceSets.testIntegration.output.classesDir
	classpath = sourceSets.testIntegration.runtimeClasspath
	outputs.upToDateWhen { false }
}

// if building the project, run integration tests if and only if all unit tests pass first
testIntegration.mustRunAfter test

// This task will utilize the test.exec generated by gradle test
task unitCoverageReport(type: JacocoReport) {
	executionData test

	sourceDirectories = files(sourceSets.main.allSource.srcDirs)
	classDirectories = files(sourceSets.main.output)

	reports {
		xml.enabled true
		html.enabled false
	}

}

// This task will utilize the testIntegration.exec generated by gradle testIntegration
task integrationCoverageReport(type: JacocoReport) {
	executionData testIntegration

	sourceDirectories = files(sourceSets.main.allSource.srcDirs)
	classDirectories = files(sourceSets.main.output)

	reports {
		xml.enabled true
		html.enabled false
	}

}

jacocoTestReport {

	reports {
		xml.enabled true
		html.enabled false
	}
}
